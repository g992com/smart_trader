//------------------------------------------------------------------------
// 简称: C_Pro_Y_Signal
// 名称: C信号
// 类别: 公式应用
// 类型: 用户应用
// 版本: 2013-4-5  23:13 初稿
//       2013-4-24 23:40 调整参数，参见改进思路2
//	 2013-4-30 0:04 调整参数，见改进思路3；修改开仓和平仓时间的表示方法，由时间改为bar编号；使用参数代替数据库读写时的键名
// 输出:
//------------------------------------------------------------------------

/*------------------------------------------------------------------------
程序定义：
多：
1、指数高于30分钟160均线；
2、30分钟KDJ金叉阶段；即时金叉无此限制）
3、30、60分钟D值均大于80不开多仓；看前一根线的KDJ。
4、开盘涨幅在0.1%至1.5%之间(含15:15.即时金叉无此限制）；
5、止损0.6%；开仓价*(1-0.6%)+(开仓价与最高价差)*30%
6、当日9:14:30以开盘价高1个点开仓；9:45-10:45即时金叉开仓；
7、突破160均线时可开日内全时段延续仓；最迟2点30分之前，及时金叉或金叉阶段
8、当日9:45金叉,次日9:15不开多仓；
9、当日15:09平仓。

空：
1、指数低于30分钟160均线；
2、30分钟KDJ死叉阶段；即时死叉开仓
3、日D值大于27；
3、30、60分钟D值均小于30不开空仓；
4、开盘跌幅在0%至-0.8%之间、涨幅>=0.3%（或涨幅0.1%-0.2%不开仓）；(涨跌幅都是按照收盘价)
5、10:15时分时均价线>＝9:45分时均价线0.08％平仓；
6、止损0.6%；开仓价*(1+0.6%)-(开仓价与最低价差)*20%
7、当日9:14:30以开盘价低1个点开仓；9:15-10.45即时死叉开仓；
8、突破160均线时可开日内全时段延续仓；
9、当日9:45死叉,次日9:15不开空仓；
10、当日15:09平仓。

改进思路：
1、改进出场策略，使用浮动止损：原止损价+(开仓价与最高/低价价差)*X%
2、修改多空相关参数：
 Numeric duod30xz(80);//做多的30分钟D值限制：80 -》Numeric duod30xz(45);//做多的30分钟D值限制：45
 Numeric duod60xz(80);//做多的60分钟D值限制：80 -》Numeric duod60xz(58);//做多的60分钟D值限制：58
 
 Numeric kongd30xz(30);//做空的30分钟D值限制：30 -》Numeric kongd30xz(10);//做空的30分钟D值限制：10
 Numeric kongd60xz(30);//做空的60分钟D值限制：30 -》Numeric kongd60xz(20);//做空的60分钟D值限制：20
 Numeric kongddaycheck(27);//做空的日D值要求：27 -》Numeric kongddaycheck(37);//做空的日D值要求：37

 Numeric dzfmin(0.1);//做多的开盘涨幅下限：0.1% -》Numeric dzfmin(0.5);//做多的开盘涨幅下限：0.5%
 Numeric dzfmax(1.5);//做多的开盘涨幅上限：1.5% -》Numeric dzfmax(1);//做多的开盘涨幅上限：1%

 Numeric kdfmax(0);//做空的开盘跌幅上限：0% -》Numeric kdfmax(0.05);//做空的开盘跌幅上限：0.05%
 Numeric kzfmin(0.3);//做空的开盘涨幅下限：0.3% -》Numeric kzfmin(0.1);//做空的开盘涨幅下限：0.1%
 
 Numeric dzs(0.6);//多止损：0.6% -》Numeric dzs(0.4);//多止损：0.4%；
 Numeric dfdzs(30);//多浮动止损：30% -》Numeric dfdzs(5);//多浮动止损：5%；
 Numeric kfdzs(20);//空浮动止损：20% -》Numeric kfdzs(15);//空浮动止损：15%
3、修改多空时间点相关参数：

Numeric dtpsjxz(16);//做多突破160均线时延续仓的最迟时间：14点45分前 -> Numeric dtpsjxz(9);//做多突破160均线时延续仓的最迟时间：13点前

Numeric ktpsjxz(16);//做空突破160均线时延续仓的最迟时间：14点45分前 -> Numeric ktpsjxz(17);//做空突破160均线时延续仓的最迟时间：15点前

//做空开仓时机：9:45~10:45 -> //做空开仓时机：9:45~13:15
Numeric kkclenth(4); -> Numeric kkclenth(8); 
-------------------------------------------------------------------------*/

Params
/*
cc：computing circle，计算周期
*/

 Numeric macc(160);//均线计算周期：160

 Numeric duod30xz(45);//做多的30分钟D值限制：45
 Numeric duod60xz(58);//做多的60分钟D值限制：58
 
 Numeric kongd30xz(10);//做空的30分钟D值限制：10
 Numeric kongd60xz(20);//做空的60分钟D值限制：20
 Numeric kongddaycheck(37);//做空的日D值要求：37

 Numeric dzfmin(0.5);//做多的开盘涨幅下限：0.5%
 Numeric dzfmax(1);//做多的开盘涨幅上限：1%
 Numeric kdfmin(-0.8);//做空的开盘跌幅下限：-0.8%
 Numeric kdfmax(0.05);//做空的开盘跌幅上限：0.05%
 Numeric kzfmin(0.1);//做空的开盘涨幅下限：0.1%
 
 Numeric kzs(0.6);//空止损：0.6%；
 Numeric dzs(0.4);//多止损：0.4%；
 Numeric dfdzs(5);//多浮动止损：5%；
 Numeric kfdzs(15);//空浮动止损：15%；

 Numeric kmazs(0.08);//10:15时分时均价线>＝9:45分时均价线0.08％平仓
 Numeric kmacheckh(0.10);//检查点为10点15的bar开盘时
 Numeric kmacheckm(0.15);
 Numeric kmah1(0.09);//9点
 Numeric kmah2(0.10);//10点
 Numeric kmam1(0.44);//44分
 Numeric kmam2(0.14);//14分
 
 //做多突破160均线时延续仓的最迟时间：13点前
 Numeric dtpsjxz(9);

 //做空突破160均线时延续仓的最迟时间：15点前
 Numeric ktpsjxz(17);
 
 //做多开仓时机：9:45~10:45
 Numeric dkcqd(2);
 Numeric dkclenth(4);

 //做空开仓时机：9:45~13:15
 Numeric kkcqd(2);
 Numeric kkclenth(8);
  
 Numeric dtag(1);
 Numeric ktag(-1);

 String keyname("CSignal");
 
 
Vars
 NumericSeries vma(0);//移动平均线
 Numeric ima(0);//计算移动平均线的循环变量
 Numeric maclose(0);//计算均线用的记录30分钟K线的收盘价
 
 NumericSeries k30(0);
 NumericSeries d30(0);
 //NumericSeries j30(0);
 
 NumericSeries k60(0);
 NumericSeries d60(0);
 //NumericSeries j60(0);
 
 NumericSeries kday(0);
 NumericSeries dday(0);
 //NumericSeries jday(0);
 
 NumericSeries zdf(0);
 
 Numeric avl1(0);
 Numeric avl2(0);
 
 Bool duokd30con(False);//30分钟KDJ金叉阶段做多
 Bool kongkd30con(False);//30分钟KDJ死叉阶段做空
 
 Bool duodcon(False);//30、60分钟D值均大于80不开多仓
 Bool kongdcon(False);//30、60分钟D值均小于30不开空仓，日D值大于27
 
 Bool duozdfcon(False);//开盘涨幅在0.1%至1.5%之间做多
 Bool kongzdfcon(False);//开盘跌幅在0%至-0.8%之间、涨幅>=0.3%开空仓
 
 Bool dvmabjcon(False);//指数高于30分钟160均线做多
 Bool kvmabjcon(False);//指数低于30分钟160均线做空
 
 Bool duovmatpcon(False);//突破160均线时可开日内全时段延续仓
 Bool kongvmatpcon(False);//突破160均线时可开日内全时段延续仓
 
 Bool nojccon(False);//当日9:45金叉,次日9:15不开多仓
 Bool nosccon(False);//当日9:45死叉,次日9:15不开空仓
 
 Bool jsjccon(False);//即时金叉开多
 Bool jssccon(False);//即时死叉开空

 NumericSeries kpcurrent(0);//记录开盘K线的CurrentBar
            
Begin 
/*-------------------------------集合竞价数据过滤------------------------*/
  If(BarStatus==2 && Date!=Date[1] && High==Low) 
    return;
 

/*-------------------------------获取开盘涨跌幅-------------------------------*/
If(Date!=Date[1])
{
	zdf=Round((Open-Close[1])/Close[1],3);
	kpcurrent=CurrentBar;
}

/*-------------------------------30分钟160均线----------------------------------*/

If(Mod(CurrentBar,2)==1)
{
	//30分钟均线
	maclose = Close;
	ima = 0;
	While((ima/2+1)<macc)
	{
		maclose = maclose + Close[ima+2];
		ima=ima+2;
	}
	vma=Round(maclose/macc,1);
}
Else
{
	//30分钟均线
	vma=vma[1];
}
//画均线
PlotNumeric("vma",vma,0,red);

/*-------------------------------获取30分钟KDJ----------------------------------*/
 k30=Value(GetTBProfileString(Symbol+"30K",Text(CurrentBar)));
 d30=Value(GetTBProfileString(Symbol+"30D",Text(CurrentBar)));
 
/*-------------------------------获取60分钟KDJ----------------------------------*/
 k60=Value(GetTBProfileString(Symbol+"60K",Text(CurrentBar)));
 d60=Value(GetTBProfileString(Symbol+"60D",Text(CurrentBar)));

/*-------------------------------获取日KDJ----------------------------------*/
 kday=Value(GetTBProfileString(Symbol+"dayK",Text(CurrentBar)));
 dday=Value(GetTBProfileString(Symbol+"dayD",Text(CurrentBar)));
 
/*-------------------------------条件判断----------------------------------------*/

//30分钟KDJ金叉阶段；30分钟KDJ死叉阶段
 if(k30[1]>d30[1])//金叉阶段
	{
	    duokd30con = True;
		kongkd30con = False;
	}
	else If(k30[1]<d30[1])//死叉阶段
	{
	    duokd30con = False;
		kongkd30con = True;
	}
	
//即时金叉
 If(CrossOver(k30[1],d30[1]))
 {
	jsjccon=True;
	PlotBool("金叉",jsjccon);
 }
//即时死叉
 If(CrossUnder(k30[1],d30[1]))
 {
	jssccon=True;
	PlotBool("死叉",jssccon);
 }

//30、60分钟D值均大于80不开多；
 if(Round(d30[1],0)>duod30xz And Round(d60[1],0)>duod60xz)
	{
	    duodcon = False;
	}else
	{
	    duodcon = True;
	}
	
//30、60分钟D值均小于30不开空仓，日D值大于27；
 if((Round(d30[1],0)<kongd30xz And Round(d60[1],0)<kongd60xz) Or Round(dday[1],0)<=kongddaycheck)
	{
	    kongdcon=False;
	}else
	{
	    kongdcon=True;
	}

//开盘涨幅在0.1%至1.5%之间(含15:15.即时金叉无此限制）；
 if(zdf>=(dzfmin/100) And zdf<=(dzfmax/100))
	{
	    duozdfcon=True;
	}else
	{
		duozdfcon=False;
	}
 
//开盘跌幅在0%至-0.8%之间、涨幅>=0.3%开仓（或涨幅0.1%-0.2%不开仓）；
 if((zdf>=(kdfmin/100) And zdf<=(kdfmax/100)) Or zdf>=(kzfmin/100))
	{
	    kongzdfcon=True;
	}else
	{
	    kongzdfcon=False;
	}

//指数高于30分钟160均线做多；指数低于30分钟160均线做空；
 if(Close[1]>vma[1])
	{
	    dvmabjcon=True;
	}else
	{
	    dvmabjcon=False;
	}
 if(Close[1]<vma[1])
	{
	    kvmabjcon=True;
	}else
	{
	    kvmabjcon=False;
	}

//突破160均线时可开日内全时段延续仓，最迟2点30分之前
 If(Close[3]<=vma[3] And Close[1]>vma[1] And Date[1]==Date And CurrentBar<(kpcurrent+dtpsjxz) And Mod(CurrentBar,2)==0)//向上突破
	{
	    duovmatpcon=True;
	    kongvmatpcon=False;
	}
 If(Close[3]>=vma[3] And Close[1]<vma[1] And Date[1]==Date And CurrentBar<(kpcurrent+ktpsjxz) And Mod(CurrentBar,2)==0)//向下突破
	{
	    duovmatpcon=False;
	    kongvmatpcon=True;
	}
	
//当日9:45金叉,次日9:15不开多仓；当日9:45死叉,次日9:15不开空仓
 If(Date!=Date[1])
 {
	if(k30[18]<=d30[18] And k30[17]>d30[17])//金叉
	{
	    nojccon=False;
	}
	else
	{
	    nojccon=True;
	}
	if(k30[18]>=d30[18] And k30[17]<d30[17])//死叉
	{
	    nosccon=False;
	}
	else
	{
	    nosccon=True;
	}
 }
 
/*---------------------------------------建仓信号-----------------------------------------------*/

//----------------------------做多-----------------------
//开盘做多
if(Date!=Date[1] And dvmabjcon And duokd30con And duodcon And duozdfcon And nojccon)
{	
	SetTBProfileString(keyname,Text(Date+Time),Text(dtag));
 }

//盘中做多，有时间限制
if((CurrentBar>=(kpcurrent+dkcqd) And CurrentBar<=(kpcurrent+dkcqd+dkclenth)) And jsjccon And dvmabjcon And duodcon)
{	
	SetTBProfileString(keyname,Text(Date+Time),Text(dtag));
}

//盘中做多，突破160均线
if(duovmatpcon And (jsjccon Or duokd30con) And dvmabjcon And duodcon)
{	
	SetTBProfileString(keyname,Text(Date+Time),Text(dtag));
}

//----------------------------做空-----------------------
//开盘做空
if(Date!=Date[1] And kvmabjcon And (kongkd30con Or jssccon) And kongdcon And kongzdfcon And nosccon)
{ 
	SetTBProfileString(keyname,Text(Date+Time),Text(ktag));
}

//盘中做空，有时间限制
if((CurrentBar>=(kpcurrent+kkcqd) And CurrentBar<=(kpcurrent+kkcqd+kkclenth)) And jssccon And kvmabjcon And kongdcon)
{ 
	SetTBProfileString(keyname,Text(Date+Time),Text(ktag));
}

//盘中做空，突破160均线
if(kongvmatpcon And (jssccon Or kongkd30con) And kvmabjcon And kongdcon)
{ 
	SetTBProfileString(keyname,Text(Date+Time),Text(ktag));
}
   
End

//------------------------------------------------------------------------
// 编译版本	GS2010.12.08
// 用户版本	2013/03/24 20:52
// 版权所有	robot84
// 更改声明	TradeBlazer Software保留对TradeBlazer平台
//			每一版本的TrabeBlazer公式修改和重写的权利
//------------------------------------------------------------------------